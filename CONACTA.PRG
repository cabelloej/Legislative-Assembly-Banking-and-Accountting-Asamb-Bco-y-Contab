***
*** ACTUALIZA CUENTAS
***
PUSH KEY CLEAR
DEFI WIND WINDCTA FROM 02,00 TO 20,79;
          TITLE " CUENTAS " ;
          FOOTER " F1=Inc, F2=Mod, F3=Eli, F4=Bus, Enter=Sal " ;
          DOUBLE NOFLOAT NOZOOM NOGROW SHADOW COLOR SCHEME 10
ACTI WIND WINDCTA
ON KEY LABEL F1 DO PROCTAI
ON KEY LABEL F2 DO PROCTAM
ON KEY LABEL F3 DO PROCTAE
ON KEY LABEL F4 DO PROCTAB
ON KEY LABEL ENTER KEYBOARD '{CTRL+W}'
SELECT CONCTAS
BROWSE FIELDS CODCTA:H="Codigo",DESCTA1:H="Descripcion",GM:H="Gen/Mvto",THPO:H="Tes/Hac/Pre",AP:h="Act/Pas";
       NOAPPEND NODELETE NOEDIT NOMENU NOOPTIMIZE REST SAVE IN WINDOW WINDCTA
DEACT WIND WINDCTA
POP KEY
RETURN

************
PROC PROCTAI
************
PUSH KEY CLEAR
STORE "INCLUIR" TO WFLAGRCTA
SCATT MEMV BLANK
DO WHILE .T.
   SELECT CONCTAS
   STORE SPACE(LEN(M.DESCTA1)) TO M.DESCTA1
   DO GETCTA
   DO SHOWCTAS
   IF LASTKEY()=27
      EXIT
   ENDIF
ENDDO
POP KEY
RETURN

************
PROC PROCTAM
************
PUSH KEY CLEAR
STORE "MODIFICAR" TO WFLAGRCTA
SELECT CONCTAS
IF .NOT. EOF()
   SCATT MEMV
   DO GETCTA
ENDIF
POP KEY
RETURN

************
PROC PROCTAE
************
SELECT CONCTAS
IF EOF()
   RETURN
ENDIF
IF DEBE>0.OR.HABER>0
   STORE "PRIMERO ELIMINE TRANSACCIONES CON ESTA CUENTA Y LUEGO REINTENTE" TO WTEXT
   DO AVISO WITH WTEXT
   RETURN
ENDIF
PUSH KEY CLEAR
STORE "ELIMINAR" TO WFLAGRCTA
STORE 2 TO WOP
DO ACEPCANC WITH WFLAGRCTA
IF WOP=1
   IF RECLOC()
      DELETE
      UNLOCK
   ELSE
      DO PROCNUL
   ENDIF
ENDIF
POP KEY
RETURN

************
PROC PROCTAB
************
PUSH KEY CLEAR
STORE "BUSCAR" TO WFLAGRCTA
DEFI WIND WINDBUS FROM 20,5 TO 22,75;
          TITLE  WFLAGRCTA ;
          DOUBLE NOFLOAT NOZOOM NOGROW SHADOW COLOR SCHEME 10
ACTI WIND WINDBUS
DO WHILE .T.
   SCATT MEMV BLANK
   @ 0,1 GET M.CODCTA PICTURE ALLTRIM(WMASKCTA) VALID VALCTA()
   READ
   IF LASTKEY()=27
      EXIT
   ENDIF
   IF M.CODCTA<>SPACE(LEN(M.CODCTA))
      SELECT CONCTAS
      SET ORDER TO CONCTAS1
      SEEK M.CODCTA
      IF FOUND()
         EXIT
      ELSE
         STORE "NO REGISTRADO, REINTENTE" TO WTEXT
         DO AVISO WITH WTEXT
         LOOP
      ENDIF
   ELSE
      @ 0,LEN(M.CODCTA)+2 GET M.DESCTA1
      READ
      IF LASTKEY()=27
         EXIT
      ENDIF
      IF M.DESCTA1<>SPACE(LEN(M.DESCTA1))
         SELECT CONCTAS
         SET ORDER TO CONCTAS2
         SEEK ALLTRIM(M.DESCTA1)
         IF FOUND()
            EXIT
         ELSE
            STORE "NO REGISTRADO, REINTENTE" TO WTEXT
            DO AVISO WITH WTEXT
            LOOP
         ENDIF
      ENDIF
      EXIT
   ENDIF
ENDDO
SELECT CONCTAS
SET ORDER TO CONCTAS1
RELE WIND WINDBUS
POP KEY
RETURN

***********
PROC GETCTA
***********
DEFI WIND WINDGETC FROM 10,10 TO 20,70;
          TITLE  WFLAGRCTA ;
          DOUBLE NOFLOAT NOZOOM NOGROW SHADOW COLOR SCHEME 10
ACTI WIND WINDGETC
DO WHILE .T.
   @ 01,01 SAY "Codigo ..............:"
   @ 02,01 SAY "Descripcion .........:"
   @ 04,01 SAY "General/Movimiento...:"
   @ 05,01 SAY "Tesoro/Hacienda/Pres.:"
   @ 06,01 SAY "Activo/Pasivo .......:"
   IF WFLAGRCTA="INCLUIR"
      @ 1,24 GET M.CODCTA PICTURE ALLTRIM(WMASKCTA) VALID VALCTA()
      READ
      IF LASTKEY()=27
         EXIT
      ENDIF
      SELECT CONCTAS
      SEEK M.CODCTA
      IF FOUND()
         @ 2,24 SAY DESCTA1
         @ 3,24 SAY DESCTA2
         STORE "CODIGO YA EXISTE" TO WTEXT
         DO AVISO WITH WTEXT
         LOOP
      ENDIF
   ELSE
      @ 1,24 SAY M.CODCTA
   ENDIF
   @ 2,24   GET M.DESCTA1 VALID VALDES()
   @ 3,24   GET M.DESCTA2
   @ 4,24   GET M.GM      VALID VALGM()
   @ 5,24   GET M.THPO    VALID VALTHPO()
   @ 6,24   GET M.AP      VALID VALAP()
   READ
   IF LASTKEY()=27
      EXIT
   ENDIF
   STORE 1 TO WOP
   DO ACEPCANC WITH WFLAGRCTA
   IF WOP=1
      SELECT CONCTAS
      IF WFLAGRCTA="INCLUIR"
         IF FILLOC()
            APPEND BLANK
            GATH MEMV
            UNLOCK ALL
         ELSE
           DO PROCNUL
         ENDIF
      ELSE
         IF RECLOC()
            GATH MEMV
            UNLOCK ALL
         ELSE
            DO PROCNUL
         ENDIF
      ENDIF
   ENDIF
   EXIT
ENDDO
RELE WIND WINDGETC
RETURN

*************
PROC SHOWCTAS
*************
ACTI WIND WINDCTA
*GO BOTT
SELECT CONCTAS
IF .NOT.EOF().AND.RECNO()>12
   SKIP - 11
ELSE
   GO TOP
ENDIF
BROWSE FIELDS CODCTA:H="Codigo",DESCTA1:H="Descripcion",GM:H="Grp/Mvto";
       NOAPPEND NODELETE NOEDIT NOMENU NOOPTIMIZE NOWAIT;
       REST SAVE IN WINDOW WINDCTA
RETURN

***********
FUNC VALDES
***********
IF M.DESCTA1<>SPACE(LEN(M.DESCTA1))
   RETURN .T.
ELSE
   RETURN .F.
ENDIF

**********
FUNC VALGM
**********
IF M.GM="G".OR.M.GM="M"
   RETURN .T.
ELSE
   RETURN .F.
ENDIF
**********
FUNC VALTHPO
**********
IF M.THPO="T".OR.M.THPO="H".OR.M.THPO="P"
   RETURN .T.
ELSE
   RETURN .F.
ENDIF
**********
FUNC VALAP
**********
IF M.AP="A".OR.M.AP="P"
   RETURN .T.
ELSE
   RETURN .F.
ENDIF


