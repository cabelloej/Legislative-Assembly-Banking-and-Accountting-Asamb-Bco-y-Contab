***
*** ACTUALIZA ASIENTOS
***
IF WTIPASI="D"
   STORE "DIFERIDOS" TO WTIPASIDES
ELSE
   STORE "ACTUALES"  TO WTIPASIDES
ENDIF
***
SELECT &WARCDET
SET RELA TO CODCTA INTO CONCTAS
***
STORE 0        TO WTOTDEBE
STORE 0        TO WTOTHABER
STORE 0        TO WSALDO
STORE SPACE(1) TO WKEY
***
PUSH KEY CLEAR
DEFI WIND WINDASI FROM 02,00 TO 09,79;
          TITLE " ASIENTOS "+WTIPASIDES ;
          DOUBLE NOFLOAT NOZOOM NOGROW COLOR SCHEME 10
DEFI WIND WINDREN FROM 10,00 TO 19,79;
          DOUBLE NOFLOAT NOZOOM NOGROW COLOR SCHEME 10
DEFI WIND WINDTOT FROM 20,00 TO 24,79;
          FOOTER " F1=Inc, F2=Mod, F3=Eli, F4=Bus, Esc/Enter=Sal ";
          DOUBLE NOFLOAT NOZOOM NOGROW COLOR SCHEME 10
ACTI WIND WINDTOT
ACTI WIND WINDREN
***
ACTI WIND WINDASI
ON KEY LABEL F1 DO PROASII
ON KEY LABEL F2 DO PROASIM
ON KEY LABEL F3 DO PROASIE
ON KEY LABEL F4 DO PROASIB
ON KEY LABEL ENTER KEYBOARD '{CTRL+W}'
SELECT &WARCGEN
SET ORDER TO &WINDGEN1
GO BOTT
STORE CODCOMG TO M.CODCOMG
BROWSE FIELDS CODCOMG:H="Numero",FECCOMG:H="Fecha",DESCOM1:H="Concepto";
       NOAPPEND NODELETE NOEDIT NOMENU REST SAVE IN WINDOW WINDASI WHEN SHOWREN()
DEACT WIND WINDASI
DEACT WIND WINDREN
DEACT WIND WINDTOT
POP KEY
RETURN
************
PROC SHOWREN
************
ACTI WIND WINDREN
STORE CODCOMG TO WKEY
STORE RECNO() TO WRECCOM
SELECT &WARCDET
SET ORDER TO &WINDDET1
GO TOP
BROWSE FIELDS WCODCTA=SUBSTR(CODCTA,1,WMASKLAR):H="Codigo cuenta",WDETCTA1=SUBSTR(DETCTA1,1,25):H="Detalle",;
              DOCTO:H="Documento",DEBE:H="Debe":P="@Z 999999999.99",HABER:H="Haber":P="@Z 999999999.99";
       NOAPPEND NODELETE NOEDIT NOMENU SAVE IN WINDOW WINDREN;
       NOWAIT;
       KEY WKEY
*SELECT &WARCGEN
DO SALDO
DO VALSAL
ACTI WIND WINDASI
RETURN

*******************************************************************************
************
PROC PROASII
************
PUSH KEY CLEAR
STORE "INCLUIR" TO WFLAGRASI
SELECT &WARCGEN
SCATT MEMV BLANK
DO WHILE .T.
   IF CONCONF.ENUMERAR="S"
      SELECT CONCONF
      IF RECLOC()
         REPLACE NUMCOM WITH NUMCOM+1
         STORE NUMCOM   TO   WNUMERO
         DO CONNUM      WITH WNUMERO
         STORE WNUMERO  TO   M.CODCOMG
         UNLOCK
      ELSE
         EXIT
      ENDIF
   ENDIF
   DO GETASI
   EXIT
ENDDO
POP KEY
RETURN

************
PROC PROASIM
************
PUSH KEY CLEAR
STORE "MODIFICAR" TO WFLAGRASI
SELECT &WARCGEN
IF .NOT. EOF()
   SCATT MEMV
   DO GETASI
ENDIF
POP KEY
RETURN

************
PROC PROASIE
************
SELECT &WARCGEN
IF EOF()
   RETURN
ENDIF
PUSH KEY CLEAR
STORE "ELIMINAR" TO WFLAGRASI
STORE 2 TO WOP
DO ACEPCANC WITH WFLAGRASI
IF WOP=1
   DO ELIASI
ENDIF
SELECT &WARCGEN
POP KEY
RETURN
***********
PROC ELIASI
***********
SELECT &WARCGEN
IF RECLOC()
   DELETE
   SELECT &WARCDET
   SET ORDER TO &WINDDET1
   DO WHILE .T.
      SEEK WKEY
      IF FOUND()
         IF RECLOC()
            DELETE
            UNLOCK
         ELSE
            DO PROCNUL
         ENDIF
      ELSE
         EXIT
      ENDIF
   ENDDO
   UNLOCK ALL
ELSE
   DO PROCNUL
ENDIF
RETURN
************
PROC PROASIB
************
PUSH KEY CLEAR
STORE "BUSCAR" TO WFLAGRASI
DEFI WIND WINDBUS FROM 04,00 TO 06,79;
          TITLE  WFLAGRASI ;
          DOUBLE NOFLOAT NOZOOM NOGROW COLOR SCHEME 10
ACTI WIND WINDBUS
SELECT &WARCGEN
SCAT MEMV BLANK
DO WHILE .T.
   STORE 0 TO WNUMERO
   @ 0,1 GET WNUMERO PICTURE "9999999"
   READ
   IF LASTKEY()=27
      EXIT
   ENDIF
   DO CONNUM WITH WNUMERO
   IF WNUMERO<>REPLICATE("0",LEN(WNUMERO))
      SET ORDER TO &WINDGEN1
      SEEK WNUMERO
      IF FOUND()
         EXIT
      ELSE
         STORE "NO REGISTRADO, REINTENTE" TO WTEXT
         DO AVISO WITH WTEXT
         LOOP
      ENDIF
   ELSE
      @ 0,LEN(M.CODCOMG)+2 GET M.FECCOMG
      READ
      IF LASTKEY()=27
         EXIT
      ENDIF
      IF M.FECCOMG<>CTOD("  -  -    ")
         SET ORDER TO &WINDGEN2
         SEEK DTOS(M.FECCOMG)
         IF FOUND()
            EXIT
         ELSE
            STORE "NO REGISTRADO, REINTENTE" TO WTEXT
            DO AVISO WITH WTEXT
            LOOP
         ENDIF
      ENDIF
      EXIT
   ENDIF
ENDDO
SELECT &WARCGEN
SET ORDER TO &WINDGEN1
RELE WIND WINDBUS
POP KEY
RETURN


***********
PROC GETASI
***********
DEFI WIND WIGETTOP FROM 02,00 TO 09,79;
          TITLE  WFLAGRASI ;
          DOUBLE NOFLOAT NOZOOM NOGROW COLOR SCHEME 10
ACTI WIND WIGETTOP
@ 1,1 SAY "Comprobante no.:"
@ 2,1 SAY "Fecha          :"
@ 3,1 SAY "Descripcion    :"
@ 4,1 SAY "                "
DO WHILE .T.
   IF WFLAGRASI="INCLUIR"
      IF CONCONF.ENUMERAR<>"S"
         @ 1,18 GET M.CODCOMG
         READ
         IF LASTKEY()=27
            EXIT
         ENDIF
      ELSE
         @ 1,18 SAY M.CODCOMG
      ENDIF
      SELECT &WARCGEN
      SEEK M.CODCOMG
      IF FOUND()
         @ 3,18 SAY DESCOM1
         @ 4,18 SAY DESCOM2
         STORE "CODIGO YA EXISTE" TO WTEXT
         DO AVISO WITH WTEXT
         LOOP
      ENDIF
   ELSE
      @ 1,18 SAY M.CODCOMG
   ENDIF
   @ 2,18 GET M.FECCOMG VALID VALFEC()
   READ
   DO GETDES
   @ 3,18 SAY M.DESCOM1
   @ 4,18 SAY M.DESCOM2
   IF LASTKEY()=27
      EXIT
   ENDIF
   SELECT &WARCGEN
   IF WFLAGRASI="INCLUIR"
      IF FILLOC()
         APPEND BLANK
         GATH MEMV
         UNLOCK ALL
      ELSE
        DO PROCNUL
      ENDIF
   ELSE
      IF RECLOC()
         GATH MEMV
         UNLOCK ALL
      ELSE
         DO PROCNUL
      ENDIF
   ENDIF
   EXIT
ENDDO
IF LASTKEY()<>27
   PUSH KEY CLEAR
   ON KEY LABEL F1  DO PRORENI
   ON KEY LABEL F2  DO PRORENM
   ON KEY LABEL F3  DO PRORENE
   STORE CODCOMG TO WKEY
   *DO SALDO
   ACTI WIND WINDREN
   SELECT &WARCDET
   ***
   BROWSE FIELDS WCODCTA=SUBSTR(CODCTA,1,WMASKLAR):H="Codigo cuenta",WDETCTA1=SUBSTR(DETCTA1,1,25):H="Detalle",;
                 DOCTO:H="Documento",DEBE:H="Debe":P="@z 999999999.99",HABER:H="Haber":P="@z 999999999.99";
          NOAPPEND NODELETE NOEDIT NOMENU REST SAVE IN WINDOW WINDREN WHEN DESCTA();
          KEY WKEY
   *SELECT &WARCGEN
   POP KEY
ENDIF
RELE WIND WIGETTOP
ACTI WIND WINDASI
DO SHOWREN
RETURN
************
PROC PRORENI
************
PUSH KEY CLEAR
STORE "INCLUIR" TO WFLAGRASI
SELECT &WARCDET
SCATT MEMV BLANK
STORE SPACE(LEN(M.DETCTA1)) TO M.DETCTA1
STORE SPACE(LEN(M.DETCTA2)) TO M.DETCTA2
DO GETREN
DO SALDO
POP KEY
RETURN
************
PROC PRORENM
************
PUSH KEY CLEAR
STORE "MODIFICAR" TO WFLAGRASI
SELECT &WARCDET
SCATT MEMV
DO GETREN
DO SALDO
POP KEY
RETURN
************
PROC PRORENE
************
SELECT &WARCDET
IF EOF()
   RETURN
ENDIF
PUSH KEY CLEAR
STORE "ELIMINAR" TO WFLAGRASI
STORE 2 TO WOP
DO ACEPCANC WITH WFLAGRASI
IF WOP=1
   IF RECLOC()
      DELETE
      UNLOCK ALL
   ELSE
      DO PROCNUL
   ENDIF
ENDIF
DO SALDO
POP KEY
RETURN
***********
PROC GETREN
***********
ACTI WIND WINDTOT
DO WHILE .T.
   STORE M.CODCOMG TO M.CODCOMD
   STORE M.FECCOMG TO M.FECCOMD
   STORE SPACE(1)  TO WDESCTA
   @ 0,0 CLEAR
   IF WFLAGRASI="INCLUIR"
      @ 0,1  GET M.CODCTA PICTURE ALLTRIM(WMASKCTA) VALID VALCTA()
      READ
      IF LASTKEY()=27
         EXIT
      ENDIF
      IF .NOT.CHECKCTA()
         LOOP
      ENDIF
   ENDIF
   @ 0,1           SAY M.CODCTA
   @ 2,1           SAY WDESCTA
   DO GETDET
   @ 0,WMASKLAR+2  SAY M.DETCTA1
   @ 1,WMASKLAR+2  SAY M.DETCTA2
   IF LASTKEY()=27
      EXIT
   ENDIF
   @ 0,WMASKLAR+2  SAY SPACE(40)
   @ 1,WMASKLAR+2  SAY SPACE(40)
   @ 0,WMASKLAR+2  SAY SUBSTR(M.DETCTA1,1,25)
   @ 1,WMASKLAR+2  SAY SUBSTR(M.DETCTA2,1,25)
   @ 0,WMASKLAR+LEN(M.DETCTA1)-15+3 GET M.DOCTO
   READ
   IF LASTKEY()=27
      EXIT
   ENDIF
   DO WHILE .T.
      STORE ABS(WTOTDEBE-WTOTHABER) TO WSALDO
      IF WFLAGRASI="INCLUIR"
         IF WSALDO<0
            STORE WSALDO TO M.DEBE
         ELSE
            IF WSALDO>0
               STORE WSALDO TO M.HABER
            ENDIF
         ENDIF
      ENDIF
      @ 0,WMASKLAR+LEN(M.DETCTA1)-15+LEN(M.DOCTO)+3 GET M.DEBE
      READ
      IF LASTKEY()=27
         EXIT
      ENDIF
      IF M.DEBE=0
         @ 0,WMASKLAR+LEN(M.DETCTA1)-15+LEN(M.DOCTO)+12+4 GET M.HABER RANGE 0.01, 999999999.99
         READ
         IF LASTKEY()=27
            EXIT
         ENDIF
      ELSE
         STORE 0 TO M.HABER
      ENDIF
      EXIT
   ENDDO
   ***
   IF LASTKEY()=27
      EXIT
   ENDIF
   STORE 1 TO WOP
   DO ACEPCANC WITH WFLAGRASI
   IF WOP=1
      SELECT &WARCDET
      IF WFLAGRASI="INCLUIR"
         IF FILLOC()
            APPEND BLANK
            GATH MEMV
            UNLOCK ALL
         ELSE
           DO PROCNUL
         ENDIF
      ELSE
         IF RECLOC()
            GATH MEMV
            UNLOCK ALL
         ELSE
            DO PROCNUL
         ENDIF
      ENDIF
   ENDIF
   EXIT
ENDDO
ACTI WIND WINDREN
RETURN

***********
FUNC VALDET
***********
IF M.DETCTA1<>SPACE(LEN(M.DETCTA1))
   RETURN .T.
ELSE
   RETURN .F.
ENDIF

***********
FUNC VALFEC
***********
IF M.FECCOMG<CONCONF.FECHAINI.OR.M.FECCOMG>CONCONF.FECHAFIN
   RETURN .F.
ELSE
   RETURN .T.
ENDIF
***********
PROC GETDES
***********
DEFI WIND WINDDES FROM 04,15 TO 20,56;
          TITLE " DESCRIPCION DEL ASIENTO ";
          DOUBLE NOFLOAT NOZOOM NOGROW COLOR SCHEME 10
ACTI WIND WINDDES
@ 00,00 GET M.DESCOM1 VALID VALDES()
@ 01,00 GET M.DESCOM2
@ 02,00 GET M.DESCOM3
@ 03,00 GET M.DESCOM4
@ 04,00 GET M.DESCOM5
@ 05,00 GET M.DESCOM6
@ 06,00 GET M.DESCOM7
@ 07,00 GET M.DESCOM8
@ 08,00 GET M.DESCOM9
@ 09,00 GET M.DESCOM10
@ 10,00 GET M.DESCOM11
@ 11,00 GET M.DESCOM12
@ 12,00 GET M.DESCOM13
@ 13,00 GET M.DESCOM14
@ 14,00 GET M.DESCOM15
READ
RELE WIND WINDDES
RETURN
***********
PROC GETDET
***********
DEFI WIND WINDDET FROM 04,15 TO 20,56;
          TITLE " DETALLE DEL MOVIMIENTO ";
          DOUBLE NOFLOAT NOZOOM NOGROW COLOR SCHEME 10
ACTI WIND WINDDET
@ 00,00 GET M.DETCTA1 VALID VALDET()
@ 01,00 GET M.DETCTA2
@ 02,00 GET M.DETCTA3
@ 03,00 GET M.DETCTA4
@ 04,00 GET M.DETCTA5
@ 05,00 GET M.DETCTA6
@ 06,00 GET M.DETCTA7
@ 07,00 GET M.DETCTA8
@ 08,00 GET M.DETCTA9
@ 09,00 GET M.DETCTA10
@ 10,00 GET M.DETCTA11
@ 11,00 GET M.DETCTA12
@ 12,00 GET M.DETCTA13
@ 13,00 GET M.DETCTA14
@ 14,00 GET M.DETCTA15
READ
RELE WIND WINDDET
RETURN
***********
FUNC VALDES
***********
IF M.DESCOM1<>SPACE(LEN(M.DESCOM1))
   RETURN .T.
ELSE
   RETURN .F.
ENDIF

***********
PROC DESCTA
***********
*
* MUESTRA  DESCRIPCION DE CUENTA EN BROWSE DE RENGLONES DEL ASIENTO
*
ACTI WIND WINDTOT
@ 0,0 SAY CONCTAS.DESCTA1
ACTI WIND WINDREN
RETURN

*************
PROC CHECKCTA
*************
*
* VERIFICA Y ASISTE EN SELECCION DE CUENTAS
*
SELECT CONCTAS
SEEK M.CODCTA
IF FOUND()
   IF GM="G"
      STORE "EL GRUPO DE CUENTA NO ES VALIDO" TO WTEXT
      DO AVISO WITH WTEXT
      STORE .F. TO WRETURN
   ELSE
      STORE DESCTA1 TO WDESCTA
      STORE .T.     TO WRETURN
   ENDIF
ELSE
   DO CONACTA
   IF GM="G"
      STORE "EL GRUPO DE CUENTA NO ES VALIDO" TO WTEXT
      DO AVISO WITH WTEXT
      STORE .F. TO WRETURN
   ELSE
      STORE CODCTA  TO M.CODCTA
      STORE DESCTA1 TO WDESCTA
      STORE .T.     TO WRETURN
   ENDIF
ENDIF
SELECT &WARCDET
RETURN WRETURN

************
PROC SALDO
************
*
* CALCULA Y DESPLEGA LOS TOTALES POR COLUMNA Y EL SALDO DEL COMPROBANTE
*
PARA WFROM
STORE 0 TO WTOTDEBE
STORE 0 TO WTOTHABER
STORE 0 TO WSALDO
STORE .T. TO WFLAGCAL
SELECT &WARCDET
SEEK WKEY
DO WHILE.NOT.EOF().AND.CODCOMD=WKEY
   IF DEBE>0
      STORE WTOTDEBE+DEBE   TO WTOTDEBE
      IF HABER>0
         STORE .F. TO WFLAGCAL
      ENDIF
   ELSE
      IF HABER<=0
         STORE .F. TO WFLAGCAL
      ELSE
         STORE WTOTHABER+HABER TO WTOTHABER
      ENDIF
   ENDIF
   SKIP
ENDDO
GO TOP
IF .NOT.WFLAGCAL
   STORE "ERROR EN MOVIMIENTOS DEL ASIENTO, VERIFIQUE CTA:"+CODCTA TO WTEXT
   DO AVISO WITH WTEXT
   STORE 0 TO WTOTDEBE
   STORE 0 TO WTOTHABER
   STORE 0 TO WSALDO
ENDIF
STORE WTOTDEBE-WTOTHABER TO WSALDO
ACTI WIND WINDTOT
@ 0,0 CLEAR
@ 0,40 SAY "Totales:"
@ 0,51 SAY WTOTDEBE
@ 0,64 SAY WTOTHABER
@ 2,40 SAY "Saldo  :"
IF WSALDO>0
   @ 2,52 SAY STR(ABS(WSALDO),12,2)
ELSE
   IF WSALDO<0
      @ 2,65 SAY STR(ABS(WSALDO),12,2)
   ENDIF
ENDIF
ACTI WIND WINDREN
RETURN

***********
PROC VALSAL
***********
*
* VALIDA SALDOS PARA TRANSFERIR ASIENTO ENTRE DIFERIDOS Y ACTUALES
*
IF WSALDO<>0
   IF WARCGEN="CONAAG"
      STORE "ASIENTO CON SALDO SERA ELIMINADO, TRANSFERIR A DIFERIDOS ? (S/N)" TO WTEXT
      STORE "SN" TO WCH
      DO PREGUNTA
      IF WCH="S"
         DO TRANSFER
         DO ELIASI
      ELSE
         DO ELIASI
      ENDIF
      ACTI WIND WINDREN
   ENDIF
ELSE
   IF WARCGEN="CONADG"
      STORE "ASIENTO SIN SALDO, DESEA TRANSFERIR A ASIENTOS ACTUALES ? (S/N)" TO WTEXT
      STORE "SN" TO WCH
      DO PREGUNTA
      IF WCH="S"
         DO TRANSFER
         DO ELIASI
      ENDIF
   ENDIF
ENDIF
RETURN

*************
PROC TRANSFER
*************
*
* TRANSFIERE ASIENTOS ENTRE DIFERIDOS Y ACTUALES
*
IF WTIPASI="D"
   STORE "CONAAG" TO WDESG
   STORE "CONAAD" TO WDESD
ELSE
   STORE "CONADG" TO WDESG
   STORE "CONADD" TO WDESD
ENDIF
SELECT &WARCGEN
SCAT MEMV
SELECT &WDESG
IF FILLOC()
   APPEND BLANK
   GATH MEMV
   UNLOCK ALL
   SELECT &WARCDET
   SET ORDER TO &WINDDET1
   SEEK WKEY
   DO WHILE.NOT.EOF().AND.CODCOMD=WKEY
      SCAT MEMV
      SELECT &WDESD
      IF FILLOC()
         APPEND BLANK
         GATH MEMV
         UNLOCK ALL
      ELSE
         DO PROCNUL
      ENDIF
      SELECT &WARCDET
      SKIP
   ENDDO
ELSE
   DO PROCNUL
ENDIF
UNLOCK ALL
RETURN



